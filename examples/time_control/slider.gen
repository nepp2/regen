
sdl_img_init(IMG_INIT_PNG);

def record_image = sdl_img_load("examples/time_control/record.png".data);
def pause_image = sdl_img_load("examples/time_control/pause.png".data);
def stop_image = sdl_img_load("examples/time_control/stop.png".data);
def onion_image = sdl_img_load("examples/time_control/onion.png".data);

type SliderView = struct {
  width : i64,
  height : i64,
  window : Window,
  record_texture : SdlTextureHandle,
  pause_texture : SdlTextureHandle,
  stop_texture : SdlTextureHandle,
  onion_texture : SdlTextureHandle,
  record_rect : SdlRect,
  stop_rect : SdlRect,
  onion_rect : SdlRect,
  bar_rect : SdlRect,
  button_size : i64,
}

type Slider = struct {
  view : SliderView,
  cursor_val : i64,
  mouse_down : bool,
  recording : bool,
  onion_skin : bool,
  events : EventList,
  events_max : i64,
}

fun rect_contains(r : SdlRect, x : i64, y : i64) {
  let rx = r.x as i64;
  let ry = r.y as i64;
  let rw = r.w as i64;
  let rh = r.h as i64;
  if x >= rx {
    if x < rx + rw {
      if y >= ry {
        if y < ry + rh {
          return true;
        }
      }
    }
  }
  false
}

fun update_slider(slider : ptr Slider, x : i64, y : i64) {
  let sr = slider.view.bar_rect;
  if rect_contains(sr, x, y) {
    let pos = x - (sr.x as i64);
    slider.cursor_val = (pos * 100) / (sr.w as i64);
    slider.recording = false;
  }
}

fun slider_events_slice(slider : ptr Slider) {
  let cursor_index = (slider.events_max * slider.cursor_val) / 100;
  cursor_index = min(slider.events.length, cursor_index);
  EventList::slice(slider.events, cursor_index)
}

fun slider_handle_click(slider : ptr Slider, x : i64, y : i64) {
  slider.mouse_down = true;
  update_slider(slider, x, y);

  let v = ref slider.view;
  if rect_contains(v.record_rect, x, y) {
    if slider.recording {
      slider.recording = false;
    }
    else {
      slider.recording = true;
      let events = slider_events_slice(slider);
      slider.events = events;
      slider.cursor_val = 100;
      slider.onion_skin = false;
    }
  }
  if rect_contains(v.stop_rect, x, y) {
    slider.recording = false;
    slider.onion_skin = false;
    EventList::clear(slider.events);
  }
  if rect_contains(v.onion_rect, x, y) {
    if slider.events.length > 0 {
      slider.recording = false;
      slider.onion_skin = !slider.onion_skin;
    }
  }
}

fun slider_handle_event(slider : ptr Slider, event : Event) {
  if event.tag != sdl_event_tag {
    return false;
  }
  // handle time events
  let e = ref event.input;
  let window_id = slider.view.window.sdl_id;
  if e.event_type == SDL_MOUSEDOWN {
    let me = to_mouse_button_event(e);
    if me.window_id == window_id {
      slider_handle_click(slider, me.x as i64, me.y as i64);
      return true;
    }
  }
  if e.event_type == SDL_MOUSEUP {
    let me = to_mouse_button_event(e);
    if me.window_id == window_id {
      slider.mouse_down = false;
      return true;
    }
  }
  if e.event_type == SDL_MOUSEMOTION {
    if slider.mouse_down {
      let mm = to_mouse_motion_event(e);
      if mm.window_id == window_id {
        update_slider(slider, mm.x as i64, mm.y as i64);
        return true;
      }
    }
  }
  false
}

fun slider_handle_events(slider : ptr Slider, es : ptr EventList) {
  let changes = false;
  for i in 0 to es.length {
    let e = EventList::get(*es, i);
    if slider.recording {
      EventList::add(slider.events, e);
      while slider.events_max < slider.events.length {
        slider.events_max = (slider.events_max * 180) / 100;
      }
      changes = true;
    }
    let change = slider_handle_event(slider, e);
    changes = changes || change;
  }
  changes
}

fun set_color(rend: SdlRendererHandle, r : i64, g : i64, b : i64) {
  sdl_set_draw_color(rend, r as u8, g as u8, b as u8, 255 as u8);
}

fun render_slider (slider : ptr Slider) {
  let r = slider.view.window.r;
  let v = ref slider.view;
  // render
  set_color(r, 200, 200, 200);
  sdl_clear(r);

  // draw the record button
  if slider.recording {
    set_color(r, 0, 255, 0);
    sdl_fill_rect(r, ref v.record_rect);
    sdl_render_copy(r, v.pause_texture, 0 as ptr SdlRect, ref v.record_rect);
  }
  else {
    sdl_render_copy(r, v.record_texture, 0 as ptr SdlRect, ref v.record_rect);
  }
  // draw the stop button
  if slider.events.length == 0 {
    set_color(r, 100, 100, 100);
    sdl_fill_rect(r, ref v.stop_rect);
  }
  sdl_render_copy(r, v.stop_texture, 0 as ptr SdlRect, ref v.stop_rect);

  // draw onion button
  if slider.events.length == 0 {
    set_color(r, 100, 100, 100);
    sdl_fill_rect(r, ref v.onion_rect);
  }
  else if slider.onion_skin {
      set_color(r, 0, 255, 0);
      sdl_fill_rect(r, ref v.onion_rect);
  }
  sdl_render_copy(r, v.onion_texture, 0 as ptr SdlRect, ref v.onion_rect);

  // draw the bar
  set_color(r, 50, 0, 50);
  sdl_fill_rect(r, ref v.bar_rect);
  let bar = v.bar_rect;
  let bar_size = ((bar.w as i64) * slider.events.length) / slider.events_max;
  let bar_rect = init SdlRect(bar.x, bar.y, bar_size as i32, bar.h);
  set_color(r, 0, 0, 255);
  sdl_fill_rect(r, ref bar_rect);

  // draw the cursor
  set_color(r, 255, 0, 255);

  let cursor_pos = ((bar.w as i64) * slider.cursor_val) / 100;
  cursor_pos = (bar.x as i64) + min(cursor_pos, bar_size);
  let cursor_rect = init SdlRect(cursor_pos as i32, bar.y, 5 as i32, bar.h);
  sdl_fill_rect(r, ref cursor_rect);

  sdl_present(r);
};

fun create_slider_view(window : Window) {
  let width = window.w;
  let height = window.h;

  let record_texture = sdl_create_texture_from_surface(window.r, record_image);
  let pause_texture = sdl_create_texture_from_surface(window.r, pause_image);
  let stop_texture = sdl_create_texture_from_surface(window.r, stop_image);
  let onion_texture = sdl_create_texture_from_surface(window.r, onion_image);

  let button_size = height;
  let record_rect = sdl_rect(0, 0, button_size, button_size);
  let stop_rect = sdl_rect(button_size, 0, button_size, button_size);
  let onion_rect = sdl_rect(button_size * 2, 0, button_size, button_size);
  let slider_offset = button_size * 3;
  let slider_length = width - slider_offset;
  let bar_rect = sdl_rect(slider_offset, 0, slider_length, height);
  init SliderView(
    width, height, window,
    record_texture, pause_texture, stop_texture, onion_texture,
    record_rect, stop_rect, onion_rect, bar_rect, button_size,
  )
}


fun create_slider(view : SliderView) {
  let events = EventList::new();
  init Slider(view, 100, false, false, false, events, 100)
}
