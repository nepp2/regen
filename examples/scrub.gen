
include(env, ref "examples/lib/prelude.gen");
include(env, ref "examples/lib/game.gen");

def Scrub = struct {
  width : i64,
  height : i64,
  window : SdlWindowHandle,
  window_id : u32,
  renderer : SdlRendererHandle,
  val : i64,
  mouse_down : bool,
}

def scrub_update = fun(scrub : ptr Scrub, ge : ptr GameEvent) {
  // handle time events
  let e = ref ge.input;
  if e.event_type == SDL_MOUSEDOWN {
    let me = to_mouse_button_event(e);
    if me.window_id == scrub.window_id {
      scrub.val = ((me.x as i64) * 100) / scrub.width;
      scrub.mouse_down = true;
      return true;
    }
  }
  if e.event_type == SDL_MOUSEUP {
    let me = to_mouse_button_event(e);
    if me.window_id == scrub.window_id {
      scrub.mouse_down = false;
      return true;
    }
  }
  if e.event_type == SDL_MOUSEMOTION {
    if scrub.mouse_down {
      let mm = to_mouse_motion_event(e);
      if mm.window_id == scrub.window_id {
        scrub.val = ((mm.x as i64) * 100) / scrub.width;
        return true;
      }
    }
  }
  false
}

def render_scrub = fun (scrub : ptr Scrub) {
  let r = scrub.renderer;
  // render
  sdl_set_draw_color(r, 0 as u8, 0 as u8, 0 as u8, 0 as u8);
  sdl_clear(r);

  sdl_set_draw_color(r, 255 as u8, 0 as u8, 0 as u8, 255 as u8);

  let w = (scrub.width / 100) * scrub.val;

  let rect = init sdl_rect(0 as i32, 0 as i32, w as i32, scrub.height as i32);

  sdl_fill_rect(r, ref rect);
  sdl_present(r);
};

def create_scrub = fun(name : string, width : i64, height : i64) {
  let window = create_game_window(name, width, height);
  let window_id = sdl_get_window_id(window);
  let renderer =
    sdl_create_renderer(window, -1 as i32, SDL_RENDERER_ACCELERATED);
  init Scrub(width, height, window, window_id, renderer, 100, false)
}
