
// example usage:
// embed define_list_type(#list_i32, #i32);

fun define_list_type(name : expr, t : expr) {
  #(def $name = {
      def T = $t;

      type inner = struct { data : ptr ($name)::T, length : i64, capacity : i64 };

      def list = named_type(sym $name, ptr ($name)::inner);

      fun new() {
        let list = malloc(type_sizeof(($name)::inner)) as ($name)::list;
        *list = init ($name)::inner(0 as ptr ($name)::T, 0, 0);
        list
      };

      fun slice(l : ($name)::list, length : i64) {
        let list = malloc(type_sizeof(($name)::inner)) as ($name)::list;
        *list = init ($name)::inner(l.data, length, l.capacity);
        list
      };

      fun clear(l : ($name)::list) {
        l.length = 0;
      };

      fun add(l : ($name)::list, v : ($name)::T) {
        if l.length == l.capacity {
          let new_cap =
            if l.capacity == 0 { 8 }
            else { l.capacity * 2 };
          let new_data = malloc(
            type_sizeof(($name)::T) * (new_cap as u64)) as ptr ($name)::T;
          for i in 0 to l.length {
            ptr_index(new_data, i) = ptr_index(l.data, i);
          }
          if l.capacity > 0 {
            free(l.data as ptr void)
          }
          l.data = new_data;
          l.capacity = new_cap;
        }
        ptr_index(l.data, l.length) = v;
        l.length = l.length + 1;
      };

      fun append(l : ($name)::list, from : ($name)::list) {
        for i in 0 to from.length {
          let v = ptr_index(from.data, i);
          ($name)::add(l, v);
        }
      };

      fun get(l : ($name)::list, i : i64) {
        ptr_index(l.data, i)
      }

      fun get_ptr(l : ($name)::list, i : i64) {
        ref ptr_index(l.data, i)
      }

      fun set(l : ($name)::list, i : i64, v : ($name)::T) {
        ptr_index(l.data, i) = v;
      }

      ($name)::list
  });
};
